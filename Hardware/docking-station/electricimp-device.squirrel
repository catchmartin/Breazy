// WS2812 "Neopixel" LED Driver
// Copyright (C) 2014 Electric Imp, inc.
//
// Uses SPI to emulate 1-wire
// http://learn.adafruit.com/adafruit-neopixel-uberguide/advanced-coding


// This class requires the use of SPI257, which must be run at 7.5MHz 
// to support neopixel timing.
const SPICLK = 7500; // kHz

// This is used for timing testing only
us <- hardware.micros.bindenv(hardware);

class NeoPixels {
    
    // This class uses SPI to emulate the newpixels' one-wire protocol. 
    // This requires one byte per bit to send data at 7.5 MHz via SPI. 
    // These consts define the "waveform" to represent a zero or one 
    ZERO            = 0xC0;
    ONE             = 0xF8;
    BYTESPERPIXEL   = 24;
    
    // when instantiated, the neopixel class will fill this array with blobs to 
    // represent the waveforms to send the numbers 0 to 255. This allows the blobs to be
    // copied in directly, instead of being built for each pixel - which makes the class faster.
    bits            = null;
    // Like bits, this blob holds the waveform to send the color [0,0,0], to clear pixels faster
    clearblob       = blob(12);
    
    // private variables passed into the constructor
    spi             = null; // imp SPI interface (pre-configured)
    frameSize       = null; // number of pixels per frame
    frame           = null; // a blob to hold the current frame

    // _spi - A configured spi (MSB_FIRST, 7.5MHz)
    // _frameSize - Number of Pixels per frame
    constructor(_spi, _frameSize) {
        this.spi = _spi;
        this.frameSize = _frameSize;
        this.frame = blob(frameSize*BYTESPERPIXEL + 1);
        this.frame[frameSize*BYTESPERPIXEL] = 0;
        
        // prepare the bits array and the clearblob blob
        initialize();
        
        clearFrame();
        writeFrame();
    }
    
    // fill the array of representative 1-wire waveforms. 
    // done by the constructor at instantiation.
    function initialize() {
        // fill the bits array first
        bits = array(256);
        for (local i = 0; i < 256; i++) {
            local valblob = blob(BYTESPERPIXEL / 3);
            valblob.writen((i & 0x80) ? ONE:ZERO,'b');
            valblob.writen((i & 0x40) ? ONE:ZERO,'b');
            valblob.writen((i & 0x20) ? ONE:ZERO,'b');
            valblob.writen((i & 0x10) ? ONE:ZERO,'b');
            valblob.writen((i & 0x08) ? ONE:ZERO,'b');
            valblob.writen((i & 0x04) ? ONE:ZERO,'b');
            valblob.writen((i & 0x02) ? ONE:ZERO,'b');
            valblob.writen((i & 0x01) ? ONE:ZERO,'b');
            bits[i] = valblob;
        }
        
        // now fill the clearblob
        for(local j = 0; j < BYTESPERPIXEL; j++) {
            clearblob.writen(ZERO, 'b');
        }
        
    }

    // sets a pixel in the frame buffer
    // but does not write it to the pixel strip
    // color is an array of the form [r, g, b]
    function writePixel(p, color) {
        frame.seek(p*BYTESPERPIXEL);
        // red and green are swapped for some reason, so swizzle them back 
        frame.writeblob(bits[color[1]]);
        frame.writeblob(bits[color[0]]);
        frame.writeblob(bits[color[2]]);    
    }
    
    // Clears the frame buffer
    // but does not write it to the pixel strip
    function clearFrame() {
        frame.seek(0);
        for (local p = 0; p < frameSize; p++) frame.writeblob(clearblob);
    }
    
    // writes the frame buffer to the pixel strip
    // ie - this function changes the pixel strip
    function writeFrame() {
        spi.write(frame);
    }
}

/* RUNTIME STARTS HERE -------------------------------------------------------*/

const NUMPIXELS = 24;
const DELAY = 0.05;

spi <- hardware.spi257;
spi.configure(MSB_FIRST, SPICLK);
pixelStrip <- NeoPixels(spi, NUMPIXELS);

pixels <- [0,0,0,0,0]
breathe_pixels <- [0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0]
currentPixel <- 0;
pAdd <- 1;

function test(d = null) { 
  pixelStrip.writePixel(pixels[0],[0,0,0]);
  for(local i = 1; i < 5; i++) {
      local b = math.pow(2, i);
      pixelStrip.writePixel(pixels[i], [ b, b / 2, b * 1.5 ]);
  }
  
  pixelStrip.writeFrame();
  if (currentPixel >= NUMPIXELS-1) currentPixel = -1;
  currentPixel += pAdd;
  
  for (local i = 0; i < 4; i++) pixels[i] = pixels[i+1];
  pixels[4] = currentPixel;
  
  imp.wakeup(DELAY, test);
} 

cycle <- 0;

breathe_number <- 0;
coeffs <- [1.0, 1.0, 1.0];
period <- 400;
new_period <- 400;
sigma_attack <- 10;
sigma_decay <- 20;

normal <- 7;
good <- 14;
bad <- 28;
charge <- 30;

function breathe(d = null) {
    local i = breathe_number;
    //local val = (math.exp(math.sin((i/200.0)*3.14159)))*10.0;
    local valAttack = 10.0*math.exp(-1.0*math.pow(i - period/2.0, 2)/(2.0*math.pow(sigma_attack, 2))) + 10.0;
    local valDecay = 10.0*math.exp(-1.0*math.pow(i - period/2.0, 2)/(2.0*math.pow(sigma_decay, 2))) + 10.0;
    for (local j = 0; j < NUMPIXELS-1; j++) {
        if (breathe_number > period/2) {
            pixelStrip.writePixel(j,[valDecay*coeffs[0],valDecay*coeffs[1],valDecay*coeffs[2]]);  
        } else {
            pixelStrip.writePixel(j,[valAttack*coeffs[0],valAttack*coeffs[1],valAttack*coeffs[2]]);
        }
    }
    pixelStrip.writeFrame();
    if (breathe_number < period) {
        breathe_number += 1;
    } else {
        breathe_number = 0;
        period = new_period;
        
        cycle += 1;
    }
    if ((cycle >= 0) && (cycle < normal)) {
        coeffs = [1.0, 1.0, 1.0];
        imp.wakeup(0, breathe);
    } else if ((cycle >= normal) && (cycle < good)) {
        coeffs = [0.5, 1.0, 0.5];
        imp.wakeup(0, breathe);
    } else if ((cycle >= good) && (cycle < bad)) {
        coeffs = [1.0, 0.2, 0.2];
        new_period = 250;
        imp.wakeup(0, breathe);
    } else if (cycle >= bad) {
        imp.wakeup(0, charge);
    }
}

indicator <- 10;

function charge(d = null) {
    
    for (local j = 0; j < NUMPIXELS-1; j++) {
        //local px = j+4 ;
        //if (px > NUMPIXELS-1) {
            //px -= (NUMPIXELS-1);
        //}
        if (j > indicator) {
            pixelStrip.writePixel(j,[100, 100, 25]);  
        } else {
            pixelStrip.writePixel(j,[20, 20, 10]);
        }
    }
    pixelStrip.writeFrame();
    if (indicator == 20) {
        indicator = 21;
    } else {
        indicator = 20;
    }
    if (cycle > 30) {
        cycle = 0;
        imp.wakeup(0, breathe);
    } else {
        cycle += 1;
        imp.wakeup(1, charge);
    }
}

function writeVals(Data){
  local t = Data[0];
  local rcoeff = Data[1];
  local gcoeff = Data[2];
  local bcoeff = Data[3];
  coeffs = [rcoeff, gcoeff, bcoeff];
  new_period = t;
}

agent.on("Data", writeVals);

breathe();
//test();